plugins {
    id 'base'
    id 'io.spring.dependency-management'
    id 'jacoco-report-aggregation'
    id 'test-report-aggregation'
    id 'org.sonarqube'
}

allprojects {
    apply plugin: 'project-report'
    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
        maven { url 'https://jakarta.oss.sonatype.org/content/groups/staging/' }
    }
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    checkstyle {
        configFile "${project.rootDir}/../eversis_checks.xml" as File
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "net.javacrumbs.shedlock:shedlock-bom:${shedlockVersion}"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << '-parameters'
        options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    }
}

sonarqube {
    properties {
        property "sonar.sourceEncoding", "UTF-8"
    }
}

clean {
    delete 'tomcat'
}
